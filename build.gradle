buildscript {
  ext {
    dependencyManagementVersion = "1.0.9.RELEASE"
    springBootVersion = "2.3.5.RELEASE"
    lombokVersion = "1.18.12"
    junitVersion = "5.6.2"
    dockerComposePluginVersion = "0.25.0"
  }

  repositories {
    jcenter()
  }

  dependencies {
    classpath "io.spring.gradle:dependency-management-plugin:${project.dependencyManagementVersion}"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.springBootVersion}"
    classpath "com.palantir.gradle.docker:gradle-docker:${project.dockerComposePluginVersion}"
  }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'application'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'com.palantir.docker'
  apply plugin: 'com.palantir.docker-compose'

  apply from: "${rootDir}/scripts/gradle/distribution.gradle"

  group = 'com.com.jmarczyk'
  version = '1.0-SNAPSHOT'
  sourceCompatibility = 11

  repositories {
    jcenter()
  }

  dockerCompose.dockerComposeFile "${rootDir}/docker-compose.yml"
  tasks.dockerComposeUp.dependsOn( 'installDist' )
  tasks.assemble.dependsOn( 'installDist' )
}

subprojects {
  jar.enabled = true
  distZip.enabled = true

  docker {
    dockerfile file( 'Dockerfile' )
    noCache true
  }

  dependencies {
    compileOnly "org.projectlombok:lombok:${project.lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${project.lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${project.lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${project.lombokVersion}"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${project.junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${project.junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${project.junitVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.15.0'
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.15.0'
  }

  test {
    useJUnitPlatform()
  }
}

assemble {
  doLast {
    delete "${buildDir}"
  }
}